<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml"
    			xmlns="*"
    			xmlns:cs="test.granite.components.*"
    			xmlns:rs="test.granite.rpc.remoting.mxml.*"
    			layout="vertical" initialize="init()">
    <mx:Script>
        <![CDATA[
            import mx.controls.Alert;
            import mx.events.CloseEvent;
            import mx.rpc.events.ResultEvent;
            import mx.rpc.events.FaultEvent;
            import mx.rpc.remoting.mxml.Operation;
            import mx.utils.ObjectUtil;
            import mx.utils.ObjectProxy;

            import org.granite.rpc.remoting.mxml.SecureRemoteObject;
            import org.granite.events.SecurityEvent;

            [Bindable]
            private var srv:SecureRemoteObject = null;
            
            [Bindable]
            public var remoteClassMsg:String = "";

            public function init():void {
                srv = new SecureRemoteObject("login"); 
                srv.addEventListener(FaultEvent.FAULT, onRemoteFault);
                srv.addEventListener(SecurityEvent.ALL, onSecurityEvent);                            
                srv.showBusyCursor = true;
            }
            
            public function onUserDataResult(event:ResultEvent):void {
                loginView.loggedIn = true;
                remoteClassMsg = event.result.toString();
            }

            public function onSecurityEvent(event:SecurityEvent):void {
                trace("onSecurityEvent: " + event);
                switch (event.type) {
                case SecurityEvent.INVALID_CREDENTIALS:
                    loginView.loginMessage = "Invalid username or password";
                    loginView.loggedIn = false;
                    break;
                case SecurityEvent.NOT_LOGGED_IN:
                    srv.logout();
                    loginView.loginMessage = "";
                    loginView.loggedIn = false;
                    break;
                case SecurityEvent.SESSION_EXPIRED:
                    srv.logout();
                    loginView.loginMessage = "Session expired";
                    loginView.loggedIn = false;
                    break;
                case SecurityEvent.ACCESS_DENIED:
                    Alert.show("You don't have required rights to execute this action\n"+event);
                    break;
                }
            }

            public function onCredentialsSet(username:String, password:String):void {
                srv.logout();
                srv.setCredentials(username, password);                           
                this.getUserStuff();                            
            }
            
            public function doLogout():void {
                srv.logout();
                loginView.loggedIn = false;
                loginView.loginMessage = "";
                remoteClassMsg = "";
            }

            private function onRemoteFault(event:FaultEvent):void {
                trace(ObjectUtil.toString(event.fault));
                Alert.show('Fault: ' + event.fault.faultCode + ' - ' + event.fault.faultString);
            }
            
            private function getUserStuff():void {
                var operation:Operation = new Operation();
                operation.name = "getUserData";
                operation.addEventListener(ResultEvent.RESULT, onUserDataResult);
                srv.operations = {getUserData: operation};
                srv.getUserData();
            }
        ]]>
    </mx:Script>

    <mx:ViewStack id="appView" selectedIndex="{loginView.loggedIn ? 1 : 0}"  width="100%" height="100%">

        <mx:VBox width="100%" height="100%" verticalAlign="middle" horizontalAlign="center" >
            <login id="loginView" onCredentialsSet="onCredentialsSet"/>
        </mx:VBox>  

        <mx:VBox id="loggedInView" width="100%" height="100%">
        	<main id="mainView" doLogout="doLogout" userName="{this.loginView.username.text}"/>      
        </mx:VBox>
        
    </mx:ViewStack>
</mx:Application>